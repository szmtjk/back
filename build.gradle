def vJavaLang = '1.8'

def javaProjects = [project(':site'), project(':common'), project(':business'), project(':boot-jdbc')]
def bootProjects = [project(':site')]

ext {
    mobBootVersion = '1.1.1'
}

buildscript {
    ext {
        springBootVersion = '1.5.12.RELEASE'
    }
    repositories {
        maven {url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

configure(allprojects) {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    version = '1.0-SNAPSHOT'

    idea.module {
        excludeDirs += [
                file('bin'),      // containing build artifacts from Eclipse (if any)
                file('work'),     // containing transient artifacts used by embedded Tomcat
                file('logs'),     // containing app logs written by embedded Tomcat
                file('data'),     // containing logs and data written by elasticsearch
                file('.settings') // containing intentionally checked-in Eclipse metadata
        ]
    }
}

configure(javaProjects) {
    apply plugin: 'java'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    targetCompatibility = vJavaLang
    sourceCompatibility = vJavaLang
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        testCompile "org.springframework:spring-test:4.1.6.RELEASE"
        compile "commons-io:commons-io:2.6"
        compile "commons-fileupload:commons-fileupload:1.3.3"
    }

    configurations {
        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
        provided
    }

    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }
    // Ensure that all Gradle-compiled classes are available to Eclipse's
    // classpath.
    eclipseClasspath.dependsOn testClasses

    // Skip generation and removal of .settings/org.eclipse.jdt.core.prefs files
    // during the normal `gradle eclipse` / `gradle cleanEclipse` lifecycle, as
    // these files have been checked in with formatting settings imported from
    // style/sagan-format.xml and style/sagan.importorder.
    // See http://www.gradle.org/docs/current/userguide/eclipse_plugin.html
    eclipseJdt.onlyIf { false }
    cleanEclipseJdt.onlyIf { false }
}

configure(bootProjects) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'idea'
    apply plugin: 'java'
}

repositories {
    mavenLocal()
    mavenCentral()
}


